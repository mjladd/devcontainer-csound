<CsoundSynthesizer>
<CsOptions>
-odac -d
</CsOptions>
<CsInstruments>
; Converted from RndTriChorus.orc and RndTriChorus.sco
; Original files preserved in same directory

sr     = 44100
kr     = 44100
ksmps  = 1
nchnls = 2


instr    1 ; Chorus with random direction triangle LFO

ilevl    = p4        ; Output level
idelay   = p5/1000   ; Delay in ms
idepth   = p6/1000   ; LFO depth in ms
irate    = p7/kr/.25 ; LFO rate
irev     = p8        ; LFO reversing rate
imix     = p9        ; Mix: 0=Dry 1=Chorus
imax     = idelay + idepth + .1
kramp    init .5

ain      soundin  "Marimba.aif"

iseed    = rnd(1)
krnd     randi  1, irev, iseed
if       krnd > 0 goto up
kramp    = kramp + irate
goto     lfo
up:
kramp    = kramp - irate
goto     lfo
lfo:
klfo     mirror  kramp, 0, 1
alfo1    upsamp  klfo
alfo2    = 1 - alfo1
achorus1 flanger  ain, alfo1*idepth + idelay, 0, imax
achorus2 flanger  ain, alfo2*idepth + idelay, 0, imax
outs1    (ain*imix + achorus1*(1 - imix))*ilevl
outs2    (ain*imix + achorus2*(1 - imix))*ilevl

endin


</CsInstruments>
<CsScore>
;Delay and Depth in ms
;Mix: 0=Dry 1=Chorus

;   Strt  Leng  Levl  Delay Depth Rate  Rvrs  Mix
i1  0.00  1.47  1.00  10.0  5.00  0.33  0.47  0.50
e

</CsScore>
</CsoundSynthesizer>
