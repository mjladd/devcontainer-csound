<CsoundSynthesizer>
<CsOptions>
-odac -d
</CsOptions>
<CsInstruments>
; Converted from gardner.orc and gardner.sco
; Original files preserved in same directory

sr        =         44100
kr        =         4410
ksmps     =         10
nchnls    =         2

; http://sound.media.mit.edu/papers.html

; To play this score you will need to have a sound file of at least four
; seconds of 44.1 K mono sound entitled soundin.11 to which the reverb will be added.

; ORCHESTRA
; Csound system for implementing reverbs based on nested all-pass filters
; similar to those used by William Gardner (MIT)
; Coded by Hans Mikelson 12/2/97
;
; 1. Noise Click
; 2. Disk Input
; 8. Simple Sum
; 9. Feedback Filter
; 10. Delay
; 11. Simple All-Pass Filter
; 12. Nested All-Pass Filter
; 13. Double Nested All-Pass Filter
; 15. Output

          zakinit   30,30

;---------------------------------------------------------------------------
; NOISE CLICK FOR TESTING THE DECAY CURVE OF THE REVERB.
          instr  1

idur      =         p3
iamp      =         p4
ioutch    =         p5
ifco      =         p6

kamp      linseg    0, .002, iamp, .002, 0, idur-.004, 0
aout      rand      kamp

afilt     butterlp  aout, ifco
          zaw       afilt, ioutch
          outs      aout, aout

          endin

;---------------------------------------------------------------------------
; DISK INPUT
          instr  2

iamp      =         p4
irate     =         p5
isndin    =         p6
ioutch    =         p7
ifco      =         p8

ain       diskin    isndin, irate
afilt     butterlp  ain, ifco
          zaw       afilt*iamp, ioutch
          outs      ain*iamp, ain*iamp

          endin

;---------------------------------------------------------------------------
; BAND-LIMITED IMPULSE
          instr  3

iamp      =         p4
ifqc      =         cpspch(p5)
ioutch    =         p6

apulse    buzz      1,ifqc, sr/2/ifqc, 1     ; AVOID ALIASING

          zaw       apulse*iamp, ioutch
          outs      apulse*iamp, apulse*iamp

          endin


;---------------------------------------------------------------------------
; SIMPLE SUM--ADD 2 CHANNELS TOGETHER AND OUTPUT TO A THIRD CHANNEL.
          instr  8

idur      =         p3
iinch1    =         p4
iinch2    =         p5
ioutch    =         p6

ain1      zar       iinch1
ain2      zar       iinch2
          zaw       ain1+ain2, ioutch

          endin

;---------------------------------------------------------------------------
; FEEDBACK FILTER
          instr  9

idur      =         p3
ifco      =         p4
igain     =         p5
iinch1    =         p6
iinch2    =         p7
ioutch    =         p8

ain1      zar       iinch1
ain2      zar       iinch2                   ; READ IN TWO CHANNELS

afilt     butterbp  ain2, ifco, ifco/2       ; BANDPASS FILTER ONE CHANNEL
          zaw       ain1+igain*afilt, ioutch ; ADJUST FILTER LEVEL ADD & OUTPUT

          endin

;---------------------------------------------------------------------------
; DELAY  -- SIMPLE DELAY
          instr  10

idur      =         p3
idtime    =         p4/1000
igain     =         p5
iinch     =         p6
ioutch    =         p7

ain       zar       iinch                    ; READ THE CHANNEL
aout      delay     ain, idtime              ; DELAY FOR TIME
          zaw       aout, ioutch             ; OUTPUT THE CHANNEL

          endin


;---------------------------------------------------------------------------
; SIMPLE ALL-PASS FILTER
          instr  11

idur      =         p3
itime     =         p4/1000
igain     =         p5
iinch     =         p6
ioutch    =         p7
adel1     init      0

ain       zar       iinch

aout      =         adel1-igain*ain          ; FEED FORWARD
adel1     delay     ain+igain*aout, itime    ; DELAY AND FEEDBACK

          zaw       aout, ioutch

          endin

;---------------------------------------------------------------------------
; SINGLE NESTED ALL-PASS FILTER
          instr  12

idur      =         p3
itime1    =         p4/1000-p6/1000
igain1    =         p5
itime2    =         p6/1000
igain2    =         p7
iinch     =         p8
ioutch    =         p9
adel1     init      0
adel2     init      0

ain       zar       iinch

asum      =         adel2 - igain2*adel1     ; FEED FORWARD (INNER ALL-PASS)
aout      =         asum - igain1*ain        ; FEED FORWARD (OUTER ALL-PASS)

adel1     delay     ain  + igain1*aout, itime1 ; FEEDBACK (OUTER ALL-PASS)
adel2     delay     adel1 + igain2*asum, itime2 ; FEEDBACK (INNER ALL-PASS)

          zaw       aout, ioutch

          endin

;---------------------------------------------------------------------------
; DOUBLE NESTED ALL-PASS FILTER (1 OUTER WITH 2 INNER IN SERIES)
          instr  13

idur      =         p3
itime1    =         p4/1000-p6/1000-p8/1000
igain1    =         p5
itime2    =         p6/1000
igain2    =         p7
itime3    =         p8/1000
igain3    =         p9
iinch     =         p10
ioutch    =         p11
adel1     init      0
adel2     init      0
adel3     init      0

ain       zar       iinch

asum1     =         adel2 - igain2*adel1     ; FIRST  INNER FEED FORWARD
asum2     =         adel3 - igain3*asum1     ; SECOND INNER FEED FORWARD
aout      =         asum2 - igain1*ain       ; OUTER  FEED FORWARD

adel1     delay     ain  + igain1*aout, itime1 ; OUTER FEEDBACK
adel2     delay     adel1 + igain2*asum1, itime2 ; FIRST INNER FEEDBACK
adel3     delay     asum1 + igain3*asum2, itime3 ; SECOND INNER FEEDBACK

          zaw       aout, ioutch

          endin

;---------------------------------------------------------------------------
; OUTPUT FOR REVERB
          instr  15

idur      =         p3
igain     =         p4
iinch     =         p5

kdclik    linseg    0, .002, igain, idur-.004, igain, .002, 0

ain       zar       iinch
          outs      ain*kdclik, -ain*kdclik  ; INVERTING ONE SIDE MAKES THE SOUND
          endin                              ; SEEM TO COME FROM ALL AROUND YOU.
                                             ; THIS MAY CAUSE SOME PROBLEMS WITH
                                             ; SURROUND SOUND SYSTEMS.

</CsInstruments>
<CsScore>
; 1. Noise Click
; 2. Disk Input
; 3. Band-Limited Impulse
; 8. Simple Sum
; 9. Feedback Filter
;10. Delay
;11. Simple All-Pass Filter
;12. Nested All-Pass Filter
;13. Double Nested All-Pass Filter
;15. Output

f1 0 16384 10 1                               ; Sine

;---------------------------------------------------------------------------
; No Reverb
;---------------------------------------------------------------------------
;   Sta  Dur  Amp  Pitch  SoundIn  OutCh  Fco
i2  0.0  4.0  .8   1      11       1      10000

;---------------------------------------------------------------------------
; Small Room
;---------------------------------------------------------------------------
;   Sta  Dur  Amp    Pitch  OutCh
;i1  4.5  .01  30000     1
;   Sta  Dur  Amp  Pitch  SoundIn  OutCh  Fco
i2  4.5  4.0  0.5  1      11       1      6000
;   Sta  Dur  Fco   Gain  InCh1  InCh2  OutCh
i9  4.5  4.5  1600  .5    1      5      2
;   Sta  Dur  Time  Gain  InCh  OutCh
i10 4.5  4.5  24    1.0   2     3
;   Sta  Dur  Time1  Gain1  Time2  Gain2  Time3  Gain3  InCh  OutCh
i13 4.5  4.5  35     .15    22     .25    8.3    .30    3     4
;   Sta  Dur  Gain   InCh
i15 4.5  4.5  .6     4
;   Sta  Dur  Time1  Gain1  Time2  Gain2  InCh  OutCh
i12 4.5  4.5  66     .08    30     .3     4     5
;   Sta  Dur  Gain   InCh
i15 4.5  4.5  .6     5

;---------------------------------------------------------------------------
; Medium Room
;---------------------------------------------------------------------------
;   Sta  Dur  Amp  Pitch  SoundIn  OutCh  Fco
i2  9.0  4.0  0.5  1      11       1      6000
;   Sta  Dur  Fco  Gain  InCh1  InCh2  OutCh
i9  9.0  4.5  1000 .4    1      10     2
;   Sta  Dur  Time1  Gain1  Time2  Gain2  Time3  Gain3  InCh  OutCh
i13 9.0  4.5  35     .25    8.3    .35    22     .45    2     3
;   Sta  Dur  Gain   InCh
i15 9.0  4.5  .5     3
;   Sta  Dur  Time  Gain  InCh  OutCh
i10 9.0  4.5  5     1.0   3     4
;   Sta  Dur  Time1  Gain1  InCh  OutCh
i11 9.0  4.5  30     .45    4     5
;   Sta  Dur  Time  Gain  InCh  OutCh
i10 9.0  4.5  67    1.0   5     6
;   Sta  Dur  Gain   InCh
i15 9.0  4.5  .5     6
;   Sta  Dur  Time  Gain  InCh  OutCh
i10 9.0  4.5  15    .4    6     7
;   Sta  Dur  InCh1  InCh2  OutCh
i8  9.0  4.5  1.2    7      8
;   Sta  Dur  Time1  Gain1  Time2  Gain2  InCh  OutCh
i12 9.0  4.5  39     .25    9.8    .35    8     9
;   Sta  Dur  Gain   InCh
i15 9.0  4.5  .5     9
;   Sta  Dur  Time  Gain  InCh  OutCh
i10 9.0  4.5  108   1.0   9     10

;---------------------------------------------------------------------------
; Large Room
;---------------------------------------------------------------------------
;   Sta  Dur  Amp  Pitch  SoundIn  OutCh  Fco
i2  13.5 4.0  0.5  1      11       1      4000
;   Sta  Dur  Fco  Gain  InCh1  InCh2  OutCh
i9  13.5 5.0  1000 .5    1      10     2
;   Sta  Dur  Time1  Gain1  InCh  OutCh
i11 13.5 5.0  8      .3     2     3
;   Sta  Dur  Time1  Gain1  InCh  OutCh
i11 13.5 5.0  12     .3     3     4
;   Sta  Dur  Time  Gain  InCh  OutCh
i10 13.5 5.0  4     1.0   4     5
;   Sta  Dur  Gain   InCh
i15 13.5 5.0  1.5   5
;   Sta  Dur  Time  Gain  InCh  OutCh
i10 13.5 5.0  17    1.0   5     6
;   Sta  Dur  Time1  Gain1  Time2  Gain2  InCh  OutCh
i12 13.5 5.0  87     .5     62     .25    6     7
;   Sta  Dur  Time  Gain  InCh  OutCh
i10 13.5 5.0  31    1.0   7     8
;   Sta  Dur  Gain   InCh
i15 13.5 5.0  .8     8
;   Sta  Dur  Time  Gain  InCh  OutCh
i10 13.5 5.0  3     1.0   8     9
;   Sta  Dur  Time1  Gain1  Time2  Gain2  Time3  Gain3  InCh  OutCh
i13 13.5 5.0  120    .5     76     .25    30     .25    9     10
;   Sta  Dur  Gain   InCh
i15 13.5 5.0  .8     10

</CsScore>
</CsoundSynthesizer>
