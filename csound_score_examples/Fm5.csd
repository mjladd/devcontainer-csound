<CsoundSynthesizer>
<CsOptions>
-odac -d
</CsOptions>
<CsInstruments>
; Converted from Fm5.orc and Fm5.sco
; Original files preserved in same directory

sr    	= 		44100
kr    	= 		4410
ksmps 	= 		10
nchnls    =         1

; =========================================================================== ;
; This instrument implements double carrier fm to impart a formant at 1500 Hz ;
; p4 = freq in Hz   p5 = amp   p6 = modulation index  p7 = fc2 amp factor     ;
; =========================================================================== ;


		instr 	1
i1  		= 		p6*p4                        ; IMAX
i2  		= 		p7                           ; FACTOR FOR REDUCING AMP OF fc2
i3  		= 		int((1500/p4)+.5)            ; FORMANT AT FREQ OF HARMONIC CLOSEST TO 1500 Hz
i3  		= 		i3 * p4                      ; fc2
i4  		= 		p4                           ; fc1
ampenv1 	linen   	i1,p3*.1,p3,p3*.9
ampenv2 	linen   	p5,.01,p3,.01
ampenv3 	linen   	ampenv2*.2,.04,p3,.04
a1      	oscili  	ampenv1,i4,1
a2      	oscili  	ampenv2,a1+p4,1
a3      	=       	i2*a1
a4      	oscili  	ampenv3,a3+i3,1
		out     	a2+a4
		endin

</CsInstruments>
<CsScore>
f1 0 512 9 1 1 0

i1    0.0     0.1    261     10000   2.6     .2
i1    0.5     0.5    440
i1    1.5     1.5    880
i1    4.0     3.0    261
e

</CsScore>
</CsoundSynthesizer>
