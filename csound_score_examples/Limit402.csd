<CsoundSynthesizer>
<CsOptions>
-odac -d
</CsOptions>
<CsInstruments>
; Converted from Limit402.orc and Limit402.sco
; Original files preserved in same directory

sr	=  44100
kr	=  44100
ksmps	=  1
nchnls	=  2


#define INPUTFILE # "infile.sf" #

	instr 1

iamp	table	0,64	; output gain
imax1	table	1,64	; max. level
idel1	table	2,64	; delay time in sec.
idec1	table	3,64	; decay speed
iatt1	table	4,64	; attack speed
ifrq1	table	5,64	; lowpass filter freq.
imax1	=  imax1*32768
klv01	init 0

ad1,ad2	soundin $INPUTFILE		; sound input
a1,a2	soundin $INPUTFILE,idel1

klx1	downsamp abs(a1)
klx2	downsamp abs(a2)
klx	=  (klx1>klx2 ? klx1:klx2)
ktmp	=  (klx>klv01 ? idec1:iatt1)
klv01	=  klv01+(klx-klv01)*ktmp
al1	upsamp klv01
al1	butterlp al1,ifrq1
klv01x	downsamp al1

a1	=  (klv01x<imax1 ? ad1:ad1*imax1/klv01x)
a2	=  (klv01x<imax1 ? ad2:ad2*imax1/klv01x)

	outs a1*iamp,a2*iamp

	endin


</CsInstruments>
<CsScore>
t 0.00	60.000

i 1	0.0000	30.000	; length in seconds

;------------------------------------------------------------------------------
f 64 0 8 -2	1.4	; output gain
		0.5	; max. level
		0.02	; time diff. between envelope and audio signal (sec.)
		0.01	; envelope decay speed (0..1)
		0.0001	; envelope attack speed
		25	; envelope lowpass filter frequency (Hz)
;------------------------------------------------------------------------------

e	; END OF SCORE

</CsScore>
</CsoundSynthesizer>
