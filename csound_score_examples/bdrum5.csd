<CsoundSynthesizer>
<CsOptions>
-odac -d
</CsOptions>
<CsInstruments>
; Converted from bdrum5.orc and bdrum5.sco
; Original files preserved in same directory

sr        =              44100                         ; SAMPLE RATE
kr        =              44100                         ; KONTROL RATE
ksmps     =              1                             ; SAMPLES/KONTROL PERIOD
nchnls    =              2                             ; NORMAL STEREO

;---------------------------------------------------------
; KICK DRUM
; CODED BY HANS MIKELSON AUGUST 25, 1999
;---------------------------------------------------------
          zakinit        50,50                         ; MAY NEED THIS LATER


;---------------------------------------------------------
; KICK DRUM 2
;---------------------------------------------------------
          instr          11

idur      =              p3                  ; DURATION
iamp      =              p4                  ; AMPLITUDE
iacc      =              p5                  ; ACCENT
irez      =              p6                  ; RESONANCE
iod       =              p7                  ; OVERDRIVE
ilowf     =              p8                  ; LOW FREQUENCY

kfenv     linseg         1000*iacc,  .02, 180, .04, 120, idur-.06, ilowf   ; FREQ ENVELOPE
kaenv     expseg         .1, .001, 1, .02, 1, .04, .7, idur-.062, .7       ; AMP ENVELOPE
kdclck    linseg         0, .002, 1, idur-.042, 1, .04, 0                  ; DECLICK
asig      rand           2                                                 ; RANDOM NUMBER

aflt      rezzy          asig, kfenv, irez*40                              ; FILTER

aout1     =              aflt*kaenv*3*iod/iacc                             ; SCALE THE SOUND

krms      rms            aout1, 1000                                       ; LIMITER, GET RMS
klim      table3         krms*.5, 5, 1                                     ; GET LIMITING VALUE
aout      =              aout1*klim*iamp*kdclck/sqrt(iod)*1.3              ; SCALE AGAIN AND OUPUT

          outs           aout, aout                                        ; OUTPUT THE SOUND

          endin

</CsInstruments>
<CsScore>


f1 0 65536 10 1
f5 0 1024 -8 1 256 1 256 .5 128 .3 128 .1 256 .1

;a 0 0 5.68
;    Sta   Dur  Amp    Accent  Q    Overdrive  LowFqc
i11  0.0   .18  30000  1       1    2          60
i11  0.5   .    .      .       <    2.5        .
i11  1.0   .    .      .       <    2          .
i11  1.5   .    .      .       1.5  3          .

i11  2.0   .18  30000  1       1    2          60
i11  2.5   .    .      .       <    2.5        .
i11  3.0   .    .      .       <    2          .
i11  3.5   .    .      .       1.5  3          .

i11  4.0   .18  30000  1       1    2          60
i11  4.5   .    .      .       <    2.5        .
i11  5.0   .    .      .       <    2          .
i11  5.5   .    .      .       1.5  3          .

i11  6.0   .18  30000  1       1    2          60
i11  6.5   .    .      .       <    2.5        .
i11  7.0   .    .      .       <    2          .
i11  7.5   .    .      .       <    2          .
i11  7.75  .25  .      .       1.5  3          .

</CsScore>
</CsoundSynthesizer>
