<CsoundSynthesizer>
<CsOptions>
-odac -d
</CsOptions>
<CsInstruments>
; Converted from simplefof.orc and simplefof.sco
; Original files preserved in same directory

sr = 44100
kr = 4410
ksmps = 10
nchnls = 1

instr 1
iamp           =         p4
ifrq           =         p5
iform          =         p6
ioct           =         p7
iband          =         p8
iris           =         p9
idur           =         p10
idec           =         p11
iolaps         =         p12
ifna           =         1
ifnb           =         2
;ar            fof       xamp, xfund, xform, koct, kband, kris, kdur, kdec, iolaps
asig           fof       iamp,  ifrq, iform, ioct, iband, iris, idur, idec, iolaps, ifna, ifnb, p3
               out       asig
               endin


               instr     2
iamp           =         p4
ifrq           =         p5
iform          =         p6
ioct           =         p7
iband          =         p8
iris           =         p9
idur           =         p10
idec           =         p11
iolaps         =         p12
ifna           =         1
ifnb           =         2
iatk           =         p13
irel           =         p14
ifrq2          =         p15
iform2         =         p16
ioct2          =         p17
iband2         =         p18
iris2          =         p19
idur2          =         p20
idec2          =         p21
kenv           linen     iamp, iatk, p3, irel
kfrq           line      ifrq, p3, ifrq2
kform          line      iform, p3, iform2
koct           line      ioct, p3, ioct2
kband          line      iband, p3, iband2
kris           line      iris, p3, iris2
kdur           line      idur, p3, idur2
kdec           line      idec, p3, idec2
;ar            fof       xamp, xfund, xform, koct, kband, kris, kdur, kdec, iolaps
asig           fof       kenv,  kfrq, kform, koct, kband, kris, kdur, kdec, iolaps, ifna, ifnb, p3
               out       asig
               endin



</CsInstruments>
<CsScore>
f1 0 4096 10 1
f2 0 1024 19 .5 .5 270 .5

;iamp     =         p4
;ifrq     =         p5
;iform    =         p6
;ioct     =         p7
;iband    =         p8
;iris     =         p9
;idur     =         p10
;idec     =         p11
;iolaps   =         p12
;ifna     =         1
;ifnb     =         2
;iatk     =         p13
;irel     =         p14
;ifrq2    =         p15
;iform2   =         p16
;ioct2    =         p17
;iband2   =         p18
;iris2    =         p19
;idur2    =         p20
;idec2    =         p21

;ar   fof xamp, xfund, xform, koct, kband, kris, kdur, kdec,iolaps
i1   0   3     10000   200  650     0    40    .003  .02    .007    5
i1   4   3     10000    50  650     0    200   .003  .02    .007    20
i1   8   3     10000   400  950     0    300   .003  .02    .007    40
s
i1   0   3     10000   100  1150    0    20    .003  .002    .007    5
i1   4   3     10000    40  750     0    400   .003  .002    .007    30
i1   8   3     10000   800  2850    0    300   .003  .002    .007    80
s
;ar   fof xamp, xfund, xform, koct, kband, kris, kdur, kdec,iolaps
i2   0   8     10000   200  650     0    40    .003  .02    .007    30   .3   .5  100  1900  0 200 .003  .01   .001
s
i2   0   5     10000   90   350    0     20   .003  .02    .07    100   .5   .7  600  300  0 500 .001  .01   .01
s
i2   0   3     5000   500  1350    0    200   .003  .8    .01    400   .4   .1  50  500  1 100 .002  .05   .001
s
i2   0   5      10    60   1950      0    400   .001 .08    .01     100   .7   .8  40  1000  0 200 .02  .05   .01

</CsScore>
</CsoundSynthesizer>
