<CsoundSynthesizer>
<CsOptions>
-odac -d
</CsOptions>
<CsInstruments>
; Converted from indust3.orc and indust3.sco
; Original files preserved in same directory

sr             =              44100
kr             =              4410
ksmps          =              10
nchnls         =              2

;------------------------------------------------------------------
; INDUSTRY
; BY HANS MIKELSON APRIL 1998
;------------------------------------------------------------------
; ORCHESTRA


;------------------------------------------------------------------
; INDUSTRIAL LOOPS
;------------------------------------------------------------------
               instr          2

idur           =              p3
iamp           =              p4*2
ifqc           =              cpspch(p5)
iplstab        =              p6
irattab        =              p7
iratrat        =              p8
ipantab        =              p9
imixtab        =              p10
iloop          =              p11
adel           init           0

kprate         oscil          1, iratrat, irattab                                    ; PULSE RATE
kamp           oscil          iamp, kprate, iplstab                                  ; AMPLITUDE PULSE
kloop          linseg         0, .005, 1, iloop-.01, 1, .005,  0, p3-iloop-.01, 0    ; GATE IN DELAY LOOP
kpan           oscil          1, 1/idur, ipantab                                     ; PANNING
kmix           oscil          1, 1/idur, imixtab                                     ; FADING

asig           rand           kamp                                                   ; NOISE SOURCE
aflt           butterbp       asig, ifqc, ifqc/4                                     ; BAND FILTER
abal           balance        aflt, asig                                             ; BRING LEVEL BACK UP

aout           =              abal*kloop+adel                                        ; GATE IN WITH FEEDBACK
adel           delay          aout, iloop                                            ; DELAY

               outs           aout*sqrt(kpan)*kmix, aout*sqrt(1-kpan)*kmix           ; OUTPUT PAN & FADE

               endin

;------------------------------------------------------------------
; SCIFI LOOPS
;------------------------------------------------------------------
               instr          3

idur           =              p3
iamp           =              p4
ifqc           =              cpspch(p5)
irattab        =              p6
iratrat        =              p7
ipantab        =              p8
imixtab        =              p9
iloop          =              p10
imodf          =              p11
imodl          =              p12
adel           init           0

krate          oscil          1, iratrat, irattab                                    ; PULSE RATE
kloop          linseg         0, .005, 1, iloop-.01, 1, .005,  0, p3-iloop-.01, 0    ; GATE IN DELAY LOOP
kpan           oscil          1, 1/idur, ipantab                                     ; PANNING
kmix           oscil          1, 1/idur, imixtab                                     ; FADING
kmodl          linseg         0, iloop/2, imodl, iloop/2, imodl/2, .01, 0, .01, 0

;                             Amp   Fqc      Car   Mod    MAmp    Wave
abal           foscil         iamp, ifqc*krate, 1,      imodf, imodl,  1             ; FM SOURCE

aout           =              abal*kloop+adel                                        ; GATE IN WITH FEEDBACK
adel           delay          aout, iloop                                            ; DELAY

               outs           aout*sqrt(kpan)*kmix, aout*sqrt(1-kpan)*kmix           ; OUTPUT PAN & FADE

               endin

;------------------------------------------------------------------
; INDUSTRIAL LOOPS
;------------------------------------------------------------------
               instr          4

idur           =              p3
iamp           =              p4*2
ifqc           =              cpspch(p5)
iplstab        =              p6
irattab        =              p7
iratrat        =              p8
ipantab        =              p9
imixtab        =              p10
ilptab         =              p11

kpan           oscil          1, 1/idur, ipantab                 ; PANNING
kmix           oscil          1, 1/idur, imixtab                 ; FADING
kloop          oscil          1, 1/idur, ilptab                  ; LOOPING
;kfc           expseg         .1, idur/2, 4, idur/2, .1          ; FQC CENTER

loop1:
kprate         oscil          1, iratrat/kloop, irattab          ; PULSE RATE
kamp           oscil          iamp, kprate, iplstab              ; AMPLITUDE PULSE

asig           rand           kamp                               ; NOISE SOURCE
;aflt          butterbp       asig, ifqc*kfc, ifqc*kfc/4         ; BAND FILTER
aflt           butterbp       asig, ifqc, ifqc/4                 ; BAND FILTER
aout           balance        aflt, asig                         ; BRING LEVEL BACK UP

;       WHEN THE TIME RUNS OUT REINITIALIZE
               timout         0, i(kloop), cont1
               reinit         loop1
cont1:

               outs           aout*sqrt(kpan)*kmix, aout*sqrt(1-kpan)*kmix      ; OUTPUT PAN & FADE

               endin


</CsInstruments>
<CsScore>
; SCORE
f1 0 8192 10 1      ; SINE

; PULSE TABLES
f10  0 1024   7  0 512 1 512 0
f11  0 1024   7  0 256 0 256 1 256 0 256 0
f12  0 1024   7  0 256 0 128 0 128 1 128 0 128 0 256 0
; RATE TABLES
f20  0 1024  -5  100  512  1000  512 100
f21  0 1024  -5  10  1024 1000
f22  0 1024  -5  100  512  10000 512  30
f23  0 1024  -5  25  1024  62
f24  0 1024  -7  500  1024  62
f25  0 1024  -5  5000 1024  1000
f26  0 1024  -7  50   512  500 512 50
f27  0 1024  -5  .5   512  2 512 .5
f28  0 1024  -5  .5   1024 4
f29  0 1024  -7  .5   250 .5 6 2 250 2 6 1 250 1 6 4 256 .5
; FM TABLES
f70  0 1024  -7  .5   250 .5 106 2 150 2 106 1 150 1 6 4 256 .5
; PAN TABLES
f30  0 1024  7  0  1024  1
f31  0 1024  7  1  1024  0
f32  0 1024  7  1 512  0 512 1
f33  0 1024  -7 .5 1024 .5
f34  0 1024  -7 0 512 1 512 0
; MIX TABLES
f40  0 1024  5  .01 256 1 512 1 256 .01
f41  0 1024  5  .01 128 1 768 1 128 .01
f42  0 1024  5  1 1024 1
; LOOP TABLE
f50  0 1024 -7  .1 1024 .1
f51  0 1024 -7  .4 1024 .4
f52  0 1024 -7  .5 1024 .1
; FILTER FCO TABLE
f60  0 1024  -7 .01 512 4 512 .01
f61  0 1024  -5 .1 512 2 512 10
f62  0 1024  -5 .1 256 2 256 .5 256 10 256 .1
f63  0 1024  -5 .2 128 2 128 .2 128 3 128 .2 128 4 128 .2 128 5 128 .2
; FM AMP TABLE
f80  0 1024  -7  1 512 4 512 1


; LOOP TABLES
;f50 30 1024 -5  .41 50 .021 206 .45 56 .012 200 .58 56 .43 200 .025 56 .11 200 .32
; GENERATE INDUSTRIAL LOOPS
;   Sta  Dur  Amp   Pitch  PulsTab  RtTab  RtRt  PanTab  MixTab  Loop
i4  0   32   6000  8.00   12       25     1     31      41      50

; LOOP TABLES
;f51 36 1024 -5  .11 50 .11 206 .15 56 .12 200 .18 56 .13 200 .15 56 .13 200 .12
; GENERATE INDUSTRIAL LOOPS
;   Sta  Dur  Amp   Pitch  PulsTab  RtTab  RtRt  PanTab  MixTab  Loop
i4  6   12   6000  9.00   12       25     2     31      41      51

;f52 42 1024 -7  .21 50 .21 206 .15 56 .12 200 .028 56 .023 200 .015 56 .0041 200 .052
;   Sta  Dur  Amp   Pitch  PulsTab  RtTab  RtRt  PanTab  MixTab  Loop
i4  12   32   4000  7.00   10       20     1     31      41      52



</CsScore>
</CsoundSynthesizer>
