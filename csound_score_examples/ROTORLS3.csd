<CsoundSynthesizer>
<CsOptions>
-odac -d
</CsOptions>
<CsInstruments>
; Converted from ROTORLS3.ORC and ROTORLS3.SCO
; Original files preserved in same directory

sr             =         44100
kr             =         2205
ksmps          =         20
nchnls         =         2

;***************************************************
; TONE WHEEL ORGAN WITH ROTATING SPEAKER
;***************************************************


instr          1                        ; ROTOR ORGAN #2

  gaorgan      init      0
  gaorgan2     init      0

  iphase       =         p2
; ikey         =         p6
  ikey         =         12*int(p5-6) + 100*(p5-6)
  ifqc         =         cpspch(p5)

; THE LOWER TONE WHEELS HAVE INCREASED ODD HARMONIC CONTENT.
  iwheel1      =         ((ikey-12) > 12 ? 1:2)
  iwheel2      =         ((ikey+7)  > 12 ? 1:2)
  iwheel3      =         (ikey         > 12 ? 1:2)
  iwheel4      =         1

  kenv         linseg    0, .01, p4, p3-.02, p4, .01, 0

  asubfund     oscil     p6,  .5*ifqc,       iwheel1, iphase/(ikey-12)
  asub3rd      oscil     p7,  1.4983*ifqc,    iwheel2, iphase/(ikey+7)
  afund        oscil     p8,  ifqc,           iwheel3, iphase/ikey
  a2nd         oscil     p9,  2*ifqc,         iwheel4, iphase/(ikey+12)
  a3rd         oscil     p10, 2.9966*ifqc,    iwheel4, iphase/(ikey+19)
  a4th         oscil     p11, 4*ifqc,         iwheel4, iphase/(ikey+24)
  a5th         oscil     p12, 5.0397*ifqc,    iwheel4, iphase/(ikey+28)
  a6th         oscil     p13, 5.9932*ifqc,    iwheel4, iphase/(ikey+31)
  a8th         oscil     p14, 8*ifqc,         iwheel4, iphase/(ikey+36)

  gaorgan      =         gaorgan + kenv*(asubfund + asub3rd + afund + a2nd + a3rd + a4th + a5th + a6th + a8th)
  gaorgan2     =         gaorgan

endin

;ROTATING SPEAKER
               instr     3

; SPEAKER PHASE OFFSET
 iofff         =         p4
; PHASE SEPARATION BETWEEN RIGHT AND LEFT
  isep         =         p5
; GLOBAL INPUT FROM ORGAN
  asig         =         gaorgan

; DISTORTION EFFECT A LAZY "S" CURVE.  USE TABLE 6 FOR MORE DISTORTION.
  asig         =         asig/40000
  aclip        tablei    asig, 5, 1, .5
  aclip        =         aclip*16000

; DELAY BUFFER FOR ROTATING SPEAKER
  aleslie      delayr    .02, 1
               delayw    aclip

; ACCELERATION
  kenv         linseg    .8, 1, 8, 2, 8, 1, .8, 2, .8, 1, 8, 1, 8
  kenvlow      linseg    .7, 2, 7, 1, 7, 2, .7, 1, .7, 2, 7, 1, 7

; UPPER DOPPLER EFFECT
  koscl        oscil     1, kenv, 1, iofff
  koscr        oscil     1, kenv, 1, iofff + isep
  kdopl        =         .01-koscl*.0002
  kdopr        =         .012-koscr*.0002
  aleft        deltapi   kdopl
  aright       deltapi   kdopr

; LOWER EFFECT
  koscllow     oscil     1, kenvlow, 1, iofff
  koscrlow     oscil     1, kenvlow, 1, iofff + isep
  kdopllow     =         .01-koscllow*.0003
  kdoprlow     =         .012-koscrlow*.0003
  aleftlow     deltapi   kdopllow
  arightlow    deltapi   kdoprlow

; FILTER EFFECT
; DIVIDE INTO THREE FREQUENCY RANGES FOR DIRECTIONAL SOUND.

;  HIGH PASS
  alfhi        butterbp  aleft,      7000, 6000
  arfhi        butterbp  aright,  7000, 6000

;  BAND PASS
  alfmid       butterbp  aleft,      3000, 2000
  arfmid       butterbp  aright,  3000, 2000

;  LOW PASS
  alflow       butterlp  aleftlow, 1000
  arflow       butterlp  arightlow,     1000

  kflohi       oscil     1, kenv, 3, iofff
  kfrohi       oscil     1, kenv, 3, iofff + isep
  kflomid      oscil     1, kenv, 4, iofff
  kfromid      oscil     1, kenv, 4, iofff + isep

; AMPLITUDE EFFECT ON LOWER SPEAKER
  kalosc       =         koscllow * .4 + 1
  karosc       =         koscrlow * .4 + 1

; ADD ALL FREQUENCY RANGES AND OUTPUT THE RESULT.
               outs      alfhi*kflohi+alfmid*kflomid+alflow*kalosc, arfhi*kfrohi+arfmid*kfromid+arflow*karosc

  gaorgan      =         0

               endin

;ROTATING SPEAKER
instr          4

; SPEAKER PHASE OFFSET
  iofff        =         p4

; PHASE SEPARATION BETWEEN RIGHT AND LEFT
  isep         =         .2

; GLOBAL INPUT FROM ORGAN
  asig         =         gaorgan2

; DISTORTION EFFECT A LAZY "S" CURVE.  USE TABLE 6 FOR MORE DISTORTION.
  asig         =         asig/40000
  aclip        tablei    asig, 5, 1, .5
  aclip        =         aclip*16000

; DELAY BUFFER FOR ROTATING SPEAKER
  aleslie      delayr    .02, 1
               delayw    aclip

; ACCELERATION
  kenv         linseg    .8, 1, 8, 2, 8, 1, .8, 2, .8, 1, 8, 1, 8
  kenvlow      linseg    .7, 2, 7, 1, 7, 2, .7, 1, .7, 2, 7, 1, 7

; UPPER DOPPLER EFFECT
  koscl        oscil     1, kenv, 1, iofff
  koscr        oscil     1, kenv, 1, iofff + isep
  kdopl        =         .01-koscl*.0002
  kdopr        =         .012-koscr*.0002
  aleft        deltapi   kdopl
  aright       deltapi   kdopr

; LOWER EFFECT
  koscllow     oscil     1, kenvlow, 1, iofff
  koscrlow     oscil     1, kenvlow, 1, iofff + isep
  kdopllow     =         .01-koscllow*.0003
  kdoprlow     =         .012-koscrlow*.0003
  aleftlow     deltapi   kdopllow
  arightlow    deltapi   kdoprlow

; FILTER EFFECT
; DIVIDE INTO THREE FREQUENCY RANGES FOR DIRECTIONAL SOUND.

;  HIGH PASS
  alfhi        butterbp  aleft,      7000, 6000
  arfhi        butterbp  aright,  7000, 6000

;  BAND PASS
  alfmid       butterbp  aleft,      3000, 2000
  arfmid       butterbp  aright,  3000, 2000

;  LOW PASS
  alflow       butterlp  aleftlow, 1000
  arflow       butterlp  arightlow,     1000

  kflohi       oscil     1, kenv, 3, iofff
  kfrohi       oscil     1, kenv, 3, iofff + isep
  kflomid      oscil     1, kenv, 4, iofff
  kfromid      oscil     1, kenv, 4, iofff + isep

; AMPLITUDE EFFECT ON LOWER SPEAKER
  kalosc       =         koscllow * .4 + 1
  karosc       =         koscrlow * .4 + 1


               outs      alfhi*kflohi+alfmid*kflomid+alflow*kalosc, arfhi*kfrohi+arfmid*kfromid+arflow*karosc

  gaorgan2     =         0

               endin


</CsInstruments>
<CsScore>
; ************************************************************************
; Tone Wheel Organ with Rotating Speaker rev. 2
; by Hans Mikelson 2/18/97
; ************************************************************************


; GEN functions **********************************************************
; Sine
f1  0   8192  10   1 .02 .01
f2  0   1024  10   1 0 .2 0 .1 0 .05 0 .02

; Rotating Speaker Filter Envelopes
f3   0    256   7  0   110  0 18 1 18 0  110 0
f4   0    256   7  0    80 .2 16 1 64 1   16 .2 80 0

; Distortion Tables
f5 0 8192   8 -.8 336 -.78  800 -.7 5920 .7  800 .78 336 .8
f6 0 8192   8 -.8 336 -.76 3000 -.7 1520 .7 3000 .76 336 .8

; score ******************************************************************

t 0 200

;  Tone Wheel Organ

;  Start Dur   Amp   Pitch SubFund Sub3rd Fund 2nd 3rd 4th 5th 6th 8th
i1   0    6    200    8.04   8       8     8    8   3   2   1   0   4
i1   0    6    .      8.11   .       .     .    .   .   .   .   .   .
i1   0    6    .      9.02   .       .     .    .   .   .   .   .   .
i1   6    1    .      8.04   .       .     .    .   .   .   .   .   .
i1   6    1    .      8.09   .       .     .    .   .   .   .   .   .
i1   6    1    .      9.01   .       .     .    .   .   .   .   .   .
i1   7    1    .      8.04   .       .     .    .   .   .   .   .   .
i1   7    1    .      8.11   .       .     .    .   .   .   .   .   .
i1   7    1    .      9.02   .       .     .    .   .   .   .   .   .
i1   8    1    .      8.04   .       .     .    .   .   .   .   .   .
i1   8    1    .      8.09   .       .     .    .   .   .   .   .   .
i1   8    1    .      9.01   .       .     .    .   .   .   .   .   .
i1   9    8    .      8.04   .       .     .    .   .   .   .   .   .
i1   9    8    .      8.08   .       .     .    .   .   .   .   .   .
i1   9    8    .      8.11   .       .     .    .   .   .   .   .   .
i1   17   16   200   10.04   8       8     8    5   3   2   1   .   3

;   Rotating Speaker
;   Start  Dur  Offset  Sep
i3    0    33.2  .5     .2
;i4    0    33.2  .1     .1

</CsScore>
</CsoundSynthesizer>
