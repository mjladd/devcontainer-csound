<CsoundSynthesizer>
<CsOptions>
-odac -d
</CsOptions>
<CsInstruments>
; Converted from gardverb.orc and gardverb.sco
; Original files preserved in same directory

sr=44100
kr=22050
ksmps=2
nchnls=2

;----------------------------------------------------------------------------------
; Csound system for implementing reverbs based on nested all-pass filters
; similar to those used by William Gardner (MIT)
; Coded by Hans Mikelson 12/2/97-3/8/98
;----------------------------------------------------------------------------------
; use floats rescaled to 16-bit integers
; 1. Noise Click
; 2. Disk Input Mono
; 3. Disk Input Stereo
; 5. Band Limited Impulse
; 8. Simple Sum
; 9. Feedback Filter
;10. Delay
;11. Simple All-Pass Filter
;12. Nested All-Pass Filter
;13. Double Nested All-Pass Filter
;15. Output
;21. Small Room Reverb
;22. Medium Room Reverb
;23. Large Room Reverb
;25. Small  Room Reverb with input control
;26. Medium Room Reverb with input control
;27. Large  Room Reverb with input control

zakinit 30,30

;----------------------------------------------------------------------------------
; Noise Click for testing the decay curve of the reverb.
;----------------------------------------------------------------------------------
       instr  1

idur   =      p3
iamp   =      p4
ioutch =      p5
ifco   =      p6

kamp   linseg 0, .002, iamp, .01, 0, idur-.012, 0
aout   rand   kamp

afilt  butterlp aout, ifco
       zaw    afilt, ioutch
       outs   aout, aout

       endin

;----------------------------------------------------------------------------------
; Disk Input Mono
;----------------------------------------------------------------------------------
       instr  2

iamp   =      p4
irate  =      p5
isndin =      p6
ioutch =      p7

ain    diskin isndin, irate
       zaw    ain, ioutch
       outs   ain*iamp, ain*iamp

       endin
;----------------------------------------------------------------------------------
; Disk Input Stereo
;----------------------------------------------------------------------------------
        instr  3

iamp    =      p4
irate   =      p5
isndin  =      p6
ioutch1 =      p7
ioutch2 =      p8

ain1, ain2 diskin isndin, irate

        zaw    ain1, ioutch1
        zaw    ain2, ioutch2
        outs   ain1*iamp, ain2*iamp

        endin

;----------------------------------------------------------------------------------
; Band-Limited Impulse
;----------------------------------------------------------------------------------
       instr  5

iamp   =      p4
ifqc   =      cpspch(p5)
ioutch =      p6

apulse buzz    1,ifqc, sr/2/ifqc, 1 ; Avoid aliasing

       zaw     apulse*iamp, ioutch
       outs    apulse*iamp, apulse*iamp

       endin

;----------------------------------------------------------------------------------
; Simple Table
;----------------------------------------------------------------------------------
       instr  6

iamp   =      p4
ifqc   =      cpspch(p5)
itab1  =      p6
ioutch =      p7

kamp   linseg 0, .005, iamp, p3-.01, iamp, .05, 0

asin1  oscil  kamp, ifqc, itab1

       zaw    asin1, ioutch

       endin



;----------------------------------------------------------------------------------
; Simple Sum--Add 2 channels together and output to a third channel.
;----------------------------------------------------------------------------------
       instr  8

idur   =      p3
iinch1 =      p4
iinch2 =      p5
ioutch =      p6

ain1   zar    iinch1
ain2   zar    iinch2
       zaw    ain1+ain2, ioutch

       endin

;----------------------------------------------------------------------------------
; Feedback Filter
;----------------------------------------------------------------------------------
       instr  9

idur   =      p3
ifco   =      p4
igain  =      p5
iinch1 =      p6
iinch2 =      p7
ioutch =      p8

ain1   zar    iinch1
ain2   zar    iinch2                   ; Read in two channels

afilt  butterbp ain2, ifco, ifco/2     ; Bandpass filter one channel
       zaw    ain1+igain*afilt, ioutch ; Adjust filter level add & output

       endin

;----------------------------------------------------------------------------------
; Delay  -- Simple Delay
;----------------------------------------------------------------------------------
       instr  10

idur   =      p3
idtime =      p4/1000
igain  =      p5
iinch  =      p6
ioutch =      p7

ain    zar    iinch          ; Read the channel
aout   delay  ain, idtime    ; Delay for time
       zaw    aout, ioutch   ; Output the channel

       endin


;----------------------------------------------------------------------------------
; Simple All-Pass Filter
;----------------------------------------------------------------------------------
       instr  11

idur   =      p3
itime  =      p4/1000
igain  =      p5
iinch  =      p6
ioutch =      p7
adel1  init   0

ain    zar    iinch

aout   =      adel1-igain*ain           ; Feed Forward
adel1  delay  ain+igain*aout, itime     ; Delay and Feedback

       zaw    aout, ioutch

       endin

;----------------------------------------------------------------------------------
; Single Nested All-Pass Filter
;----------------------------------------------------------------------------------
       instr  12

idur   =      p3
itime1 =      p4/1000-p6/1000
igain1 =      p5
itime2 =      p6/1000
igain2 =      p7
iinch  =      p8
ioutch =      p9
adel1  init   0
adel2  init   0

ain    zar    iinch

asum   =      adel2 - igain2*adel1         ; Feed Forward (Inner all-pass)
aout   =      asum  - igain1*ain           ; Feed Forward (Outer all-pass)

adel1  delay  ain   + igain1*aout, itime1  ; Feedback (Outer all-pass)
adel2  delay  adel1 + igain2*asum, itime2  ; Feedback (Inner all-pass)

       zaw    aout, ioutch

       endin

;----------------------------------------------------------------------------------
; Double Nested All-Pass Filter (1 outer with 2 inner in series)
;----------------------------------------------------------------------------------
       instr  13

idur   =      p3
itime1 =      p4/1000-p6/1000-p8/1000
igain1 =      p5
itime2 =      p6/1000
igain2 =      p7
itime3 =      p8/1000
igain3 =      p9
iinch  =      p10
ioutch =      p11
adel1  init   0
adel2  init   0
adel3  init   0

ain    zar    iinch

asum1  =      adel2 - igain2*adel1         ; First  Inner Feed Forward
asum2  =      adel3 - igain3*asum1         ; Second Inner Feed Forward
aout   =      asum2 - igain1*ain           ; Outer  Feed Forward

adel1  delay  ain   + igain1*aout, itime1  ; Outer Feedback
adel2  delay  adel1 + igain2*asum1, itime2 ; First Inner Feedback
adel3  delay  asum1 + igain3*asum2, itime3 ; Second Inner Feedback

       zaw    aout, ioutch

       endin

;----------------------------------------------------------------------------------
; Single Nested All-Pass Filter
;----------------------------------------------------------------------------------
       instr  14

idur   =      p3
itime1 =      p4/1000-p6/1000
igain1 =      p5
itime2 =      p6/1000
igain2 =      p7
iinch  =      p8
ioutch =      p9
ifco   =      p10
adel1  init   0
adel2  init   0

ain    zar    iinch

asum   =         adel2 - igain2*adel1         ; Feed Forward (Inner all-pass)
aout   butterlp  asum  - igain1*ain, ifco     ; Feed Forward (Outer all-pass)

adel1  delay  ain   + igain1*aout, itime1  ; Feedback (Outer all-pass)
adel2  delay  adel1 + igain2*asum, itime2  ; Feedback (Inner all-pass)

       zaw    aout, ioutch

       endin

;----------------------------------------------------------------------------------
; 2D Echos
;----------------------------------------------------------------------------------
        instr    17

idur    =        p3
iamp    =        p4
iex     =        p5
iey     =        p6
isx     =        p7
isy     =        p8
iwx     =        p9
iwy     =        p10
iinch   =        p11
ioutch1 =        p12
ioutch2 =        p13
ipi     =        3.14159265
inv2pi  =        1/2/3.14159265

kamp    linseg   0, .002, iamp, idur-.004, iamp, .002, 0

ain     zar      iinch

iemsx   =        iex-isx
iemsy   =        iey-isy
iepsx   =        iex+isx
iepsy   =        iey+isy
iw2x    =        2*iwx-iex-isx
iw2y    =        2*iwy-iey-isy

iang0   =        tan(iemsx/iemsy)
idist0  =        sqrt(iemsx*iemsx+iemsy*iemsy)
iang1   =        tan(iemsx/iepsy)
idist1  =        sqrt(iemsx*iemsx+iepsy*iepsy)
iang2   =        tan(iemsy/iepsx)
idist2  =        sqrt(iemsy*iemsy+iepsx*iepsx)
iang3   =        tan(iemsx/iw2y)
idist3  =        sqrt(iemsx*iemsx+iw2y*iw2y)
iang4   =        tan(iemsy/iw2x)
idist4  =        sqrt(iemsy*iemsy+iw2x*iw2x)

adel0   delay    ain/(1+idist0/2), idist0/333
adel1   delay    ain/(1+idist1/4), idist1/333
adel2   delay    ain/(1+idist2/4), idist2/333
adel3   delay    ain/(1+idist3/4), idist3/333
adel4   delay    ain/(1+idist4/4), idist4/333

;al1, ar1 hrtfer   adel1, kang1, 0, "hrtfcomp"
;al2, ar2 hrtfer   adel2, kang2, 0, "hrtfcomp"
;al3, ar3 hrtfer   adel3, kang3, 0, "hrtfcomp"
;al4, ar4 hrtfer   adel4, kang4, 0, "hrtfcomp"

al0     =         adel0*sqrt(  (iang0+ipi)*inv2pi)
ar0     =         adel0*sqrt(1-(iang0+ipi)*inv2pi)
al1     =         adel1*sqrt(  (iang1+ipi)*inv2pi)
ar1     =         adel1*sqrt(1-(iang1+ipi)*inv2pi)
al2     =         adel2*sqrt(  (iang2+ipi)*inv2pi)
ar2     =         adel2*sqrt(1-(iang2+ipi)*inv2pi)
al3     =         adel3*sqrt(  (iang3+ipi)*inv2pi)
ar3     =         adel3*sqrt(1-(iang3+ipi)*inv2pi)
al4     =         adel4*sqrt(  (iang4+ipi)*inv2pi)
ar4     =         adel4*sqrt(1-(iang4+ipi)*inv2pi)

aoutl   =         (al0+al1+al2+al3+al4)*kamp
aoutr   =         (ar0+ar1+ar2+ar3+ar4)*kamp

        outs      aoutl, aoutr
        zaw       aoutl, ioutch1
        zaw       aoutr, ioutch2

        endin

;----------------------------------------------------------------------------------
; 2D Echos Rotating
;----------------------------------------------------------------------------------
        instr    18

idur    =        p3
iamp    =        p4
kex     init     p5
key     init     p6
isx     init     p7
isy     init     p8
iwx     init     p9
iwy     init     p10
iinch   =        p11
ioutch1 =        p12
ioutch2 =        p13
ipi     =        3.14159265
inv2pi  =        1/2/3.14159265
imaxdel =        sqrt(iwx*iwx+iwy*iwy)*2

kamp    linseg   0, .002, iamp, idur-.004, iamp, .002, 0
kramp   linseg   .3, .2, 1, p3/2-.2, .3, .2, 1, p3/2-.2, .5

ks1     oscil    20, kramp, 1
kc1     oscil    20, kramp, 1, .25
ksx     =        kc1+isx
ksy     =        ks1+isy

ain     zar      iinch

kemsx   =        kex-ksx
kemsy   =        key-ksy
kepsx   =        kex+ksx
kepsy   =        key+ksy
kw2x    =        2*iwx-kex-ksx
kw2y    =        2*iwy-key-ksy

kang0   =        tan(kemsx/kemsy)
kdist0  =        sqrt(kemsx*kemsx+kemsy*kemsy)
kang1   =        tan(kemsx/kepsy)
kdist1  =        sqrt(kemsx*kemsx+kepsy*kepsy)
kang2   =        tan(kemsy/kepsx)
kdist2  =        sqrt(kemsy*kemsy+kepsx*kepsx)
kang3   =        tan(kemsx/kw2y)
kdist3  =        sqrt(kemsx*kemsx+kw2y*kw2y)
kang4   =        tan(kemsy/kw2x)
kdist4  =        sqrt(kemsy*kemsy+kw2x*kw2x)

adeli0  vdelay    ain, kdist0/.333, imaxdel
adeli1  vdelay    ain, kdist1/.333, imaxdel
adeli2  vdelay    ain, kdist2/.333, imaxdel
adeli3  vdelay    ain, kdist3/.333, imaxdel
adeli4  vdelay    ain, kdist4/.333, imaxdel

adel0   =         adeli0/(1+kdist0/4)
adel1   =         adeli1/(1+kdist1/4)
adel2   =         adeli2/(1+kdist2/4)
adel3   =         adeli3/(1+kdist3/4)
adel4   =         adeli4/(1+kdist4/4)

;al1, ar1 hrtfer   adel1, kang1, 0, "hrtfcomp"
;al2, ar2 hrtfer   adel2, kang2, 0, "hrtfcomp"
;al3, ar3 hrtfer   adel3, kang3, 0, "hrtfcomp"
;al4, ar4 hrtfer   adel4, kang4, 0, "hrtfcomp"

al0     =         adel0*sqrt(  (kang0+ipi)*inv2pi)
ar0     =         adel0*sqrt(1-(kang0+ipi)*inv2pi)
al1     =         adel1*sqrt(  (kang1+ipi)*inv2pi)
ar1     =         adel1*sqrt(1-(kang1+ipi)*inv2pi)
al2     =         adel2*sqrt(  (kang2+ipi)*inv2pi)
ar2     =         adel2*sqrt(1-(kang2+ipi)*inv2pi)
al3     =         adel3*sqrt(  (kang3+ipi)*inv2pi)
ar3     =         adel3*sqrt(1-(kang3+ipi)*inv2pi)
al4     =         adel4*sqrt(  (kang4+ipi)*inv2pi)
ar4     =         adel4*sqrt(1-(kang4+ipi)*inv2pi)

aoutl   =         (al0+al1+al2+al3+al4)*kamp
aoutr   =         (ar0+ar1+ar2+ar3+ar4)*kamp

        outs      aoutl, aoutr
        zaw       (adeli0+adeli1+adeli2), ioutch1
        zaw       (adeli0+adeli3+adeli4), ioutch2

        endin

;----------------------------------------------------------------------------------
; Small Room Reverb
;----------------------------------------------------------------------------------
       instr  21

idur   =        p3
iamp   =        p4
iinch  =        p5

aout41 init     0
adel01 init     0
adel11 init     0
adel21 init     0
adel22 init     0
adel23 init     0
adel41 init     0
adel42 init     0

kdclick linseg  0, .002, iamp, idur-.004, iamp, .002, 0

; Initialize
asig0  zar      iinch
aflt01 butterlp asig0, 6000            ; Pre-Filter
aflt02 butterbp .5*aout41, 1600, 800   ; Feed-Back Filter
asum01  =       aflt01+.5*aflt02       ; Initial Mix

; Delay 1
adel11  delay   asum01, .024

; Double Nested All-Pass
asum21  =       adel22-.25*adel21        ; First Inner Feedforward
asum22  =       adel23-.30*asum21        ; Second Inner Feedforward
aout21  =       asum22-.15*adel11        ; Outer Feedforward
adel21  delay   adel11+.15*aout21, .0047 ; Outer Feedback
adel22  delay   adel21+.25*asum21, .022  ; First Inner Feedback
adel23  delay   asum21+.30*asum22, .0083 ; Second Inner Feedback

; Single Nested All-Pass
asum41  =       adel42-.3*adel41         ; Inner Feedforward
aout41  =       asum41-.08*aout21        ; Outer Feedforward
adel41  delay   aout21+.08*aout41, .036  ; Outer Feedback
adel42  delay   adel41+.3*asum41,  .030  ; Inner Feedback

; Output
aout    =       .6*aout41+.5*aout21

        outs    aout*kdclick, -aout*kdclick

        endin

;----------------------------------------------------------------------------------
; Medium Room Reverb
;----------------------------------------------------------------------------------
       instr  22

idur   =        p3
iamp   =        p4
iinch  =        p5

adel71 init     0
adel11 init     0
adel12 init     0
adel13 init     0
adel31 init     0
adel61 init     0
adel62 init     0

kdclick linseg  0, .002, iamp, idur-.004, iamp, .002, 0

; Initialize
asig0  zar      iinch
aflt01 butterlp asig0, 6000              ; Pre-Filter
aflt02 butterbp .4*adel71, 1000, 500     ; Feed-Back Filter
asum01  =       aflt01+.5*aflt02         ; Initial Mix

; Double Nested All-Pass
asum11  =       adel12-.35*adel11        ; First  Inner Feedforward
asum12  =       adel13-.45*asum11        ; Second Inner Feedforward
aout11  =       asum12-.25*asum01        ; Outer Feedforward
adel11  delay   asum01+.25*aout11, .0047 ; Outer Feedback
adel12  delay   adel11+.35*asum11, .0083 ; First  Inner Feedback
adel13  delay   asum11+.45*asum12, .022  ; Second Inner Feedback

adel21  delay   aout11, .005             ; Delay 1

; All-Pass 1
asub31  =       adel31-.45*adel21        ; Feedforward
adel31  delay   adel21+.45*asub31,.030   ; Feedback

adel41  delay   asub31, .067             ; Delay 2
adel51  delay   .4*adel41, .015          ; Delay 3
aout51  =       aflt01+adel41

; Single Nested All-Pass
asum61  =       adel62-.35*adel61        ; Inner Feedforward
aout61  =       asum61-.25*aout51        ; Outer Feedforward
adel61  delay   aout51+.25*aout61, .0292 ; Outer Feedback
adel62  delay   adel61+.35*asum61, .0098 ; Inner Feedback

aout    =       .5*aout11+.5*adel41+.5*aout61 ; Combine Outputs

adel71  delay   aout61, .108                  ; Delay 4

        outs    aout*kdclick, -aout*kdclick   ; Final Output

        endin

;----------------------------------------------------------------------------------
; Large Room Reverb
;----------------------------------------------------------------------------------
       instr  23

idur   =        p3
iamp   =        p4
iinch  =        p5

aout91 init     0
adel01 init     0
adel11 init     0
adel51 init     0
adel52 init     0
adel91 init     0
adel92 init     0
adel93 init     0

kdclick linseg  0, .002, iamp, idur-.004, iamp, .002, 0

; Initialize
asig0  zar      iinch
aflt01 butterlp asig0, 4000             ; Pre-Filter
aflt02 butterbp .5*aout91, 1000, 500    ; Feed-Back Filter
asum01  =       aflt01+.5*aflt02        ; Initial Mix

; All-Pass 1
asub01  =       adel01-.3*asum01        ; Feedforward
adel01  delay   asum01+.3*asub01,.008   ; Feedback

; All-Pass 2
asub11  =       adel11-.3*asub01        ; Feedforward
adel11  delay   asub01+.3*asub11,.012   ; Feedback

adel21  delay   asub11, .004            ; Delay 1
adel41  delay   adel21, .017            ; Delay 2

; Single Nested All-Pass
asum51  =       adel52-.25*adel51       ; Inner Feedforward
aout51  =       asum51-.5*adel41        ; Outer Feedforward
adel51  delay   adel41+.5*aout51,  .025 ; Outer Feedback
adel52  delay   adel51+.25*asum51, .062 ; Inner Feedback

adel61  delay   aout51, .031            ; Delay 3
adel81  delay   adel61, .003            ; Delay 4

; Double Nested All-Pass
asum91  =       adel92-.25*adel91       ; First  Inner Feedforward
asum92  =       adel93-.25*asum91       ; Second Inner Feedforward
aout91  =       asum92-.5*adel81        ; Outer Feedforward
adel91  delay   adel81+.5*aout91, .120  ; Outer Feedback
adel92  delay   adel91+.25*asum91, .076 ; First  Inner Feedback
adel93  delay   asum91+.25*asum92, .030 ; Second Inner Feedback

aout    =       .8*aout91+.8*adel61+1.5*adel21 ; Combine outputs

        outs    aout*kdclick, -aout*kdclick    ; Final Output

        endin

;----------------------------------------------------------------------------------
; Small Room Reverb with controls
;----------------------------------------------------------------------------------
       instr  25

idur   =        p3
iamp   =        p4
iinch  =        p5
idecay =        p6
idense  =       p7
idense2 =       p8
ipreflt =       p9
ihpfqc  =       p10
ilpfqc  =       p11
ioutch  =       p12

aout41 init     0
adel01 init     0
adel11 init     0
adel21 init     0
adel22 init     0
adel23 init     0
adel41 init     0
adel42 init     0

kdclick linseg  0, .002, iamp, idur-.004, iamp, .002, 0

; Initialize
asig0  zar      iinch
aflt01 butterlp asig0, ipreflt            ; Pre-Filter
aflt02 butterhp .5*aout41*idense2, ihpfqc ; Feed-Back Filter
aflt03 butterlp .5*aflt02, ilpfqc         ; Feed-Back Filter
asum01  =       aflt01+.5*aflt03          ; Initial Mix

; Delay 1
adel11  delay   asum01, .024*idecay

; Double Nested All-Pass
asum21  =       adel22-.25*adel21*idense               ; First Inner Feedforward
asum22  =       adel23-.30*asum21*idense               ; Second Inner Feedforward
aout21  =       asum22-.15*adel11*idense               ; Outer Feedforward
adel21  delay   adel11+.15*aout21*idense, .0047*idecay ; Outer Feedback
adel22  delay   adel21+.25*asum21*idense, .0220*idecay ; First Inner Feedback
adel23  delay   asum21+.30*asum22*idense, .0083*idecay ; Second Inner Feedback

; Single Nested All-Pass
asum41  =       adel42-.30*adel41*idense               ; Inner Feedforward
aout41  =       asum41-.08*aout21*idense               ; Outer Feedforward
adel41  delay   aout21+.08*aout41*idense, .036*idecay  ; Outer Feedback
adel42  delay   adel41+.30*asum41*idense, .030*idecay  ; Inner Feedback

; Output
aout    =       .6*aout41+.5*aout21
        zaw      aout*kdclick, ioutch


        endin

;----------------------------------------------------------------------------------
; Medium Room Reverb with controls
;----------------------------------------------------------------------------------
       instr  26

idur   =        p3
iamp   =        p4
iinch  =        p5
idecay =        p6
idense  =       p7
idense2 =       p8
ipreflt =       p9
ihpfqc  =       p10
ilpfqc  =       p11
ioutch  =       p12

adel71 init     0
adel11 init     0
adel12 init     0
adel13 init     0
adel31 init     0
adel61 init     0
adel62 init     0

kdclick linseg  0, .002, iamp, idur-.004, iamp, .002, 0

; Initialize
asig0  zar      iinch
aflt01 butterlp asig0, ipreflt                         ; Pre-Filter
aflt02 butterhp .4*adel71*idense2, ihpfqc              ; Feed-Back Filter
aflt03 butterlp .4*aflt02, ilpfqc                      ; Feed-Back Filter
asum01  =       aflt01+.5*aflt03                       ; Initial Mix

; Double Nested All-Pass
asum11  =       adel12-.35*adel11*idense               ; First  Inner Feedforward
asum12  =       adel13-.45*asum11*idense               ; Second Inner Feedforward
aout11  =       asum12-.25*asum01*idense               ; Outer Feedforward
adel11  delay   asum01+.25*aout11*idense, .0047*idecay ; Outer Feedback
adel12  delay   adel11+.35*asum11*idense, .0083*idecay ; First  Inner Feedback
adel13  delay   asum11+.45*asum12*idense, .0220*idecay ; Second Inner Feedback

adel21  delay   aout11, .005*idecay                    ; Delay 1

; All-Pass 1
asub31  =       adel31-.45*adel21*idense               ; Feedforward
adel31  delay   adel21+.45*asub31*idense, .030*idecay  ; Feedback

adel41  delay   asub31, .067*idecay                    ; Delay 2
adel51  delay   .4*adel41, .015*idecay                 ; Delay 3
aout51  =       aflt01+adel41

; Single Nested All-Pass
asum61  =       adel62-.35*adel61*idense               ; Inner Feedforward
aout61  =       asum61-.25*aout51*idense               ; Outer Feedforward
adel61  delay   aout51+.25*aout61*idense, .0292*idecay ; Outer Feedback
adel62  delay   adel61+.35*asum61*idense, .0098*idecay ; Inner Feedback

aout    =       .5*aout11+.5*adel41+.5*aout61 ; Combine Outputs

adel71  delay   aout61, .108*idecay                  ; Delay 4

        zaw      aout*kdclick, ioutch

        endin

;----------------------------------------------------------------------------------
; Large Room Reverb
;----------------------------------------------------------------------------------
       instr    27

idur   =        p3
iamp   =        p4
iinch  =        p5
idecay =        p6
idense  =       p7
idense2 =       p8
ipreflt =       p9
ihpfqc  =       p10
ilpfqc  =       p11
ioutch  =       p12

aout91 init     0
adel01 init     0
adel11 init     0
adel51 init     0
adel52 init     0
adel91 init     0
adel92 init     0
adel93 init     0

kdclick linseg  0, .002, iamp, idur-.004, iamp, .002, 0

; Initialize
asig0  zar      iinch
aflt01 butterlp asig0, ipreflt             ; Pre-Filter
aflt02 butterhp .5*aout91, ihpfqc          ; Feed-Back Filter
aflt03 butterlp aflt02, ilpfqc             ; Feed-Back Filter
asum01  =       aflt01+.5*idense2*aflt03   ; Initial Mix

; All-Pass 1
asub01  =       adel01-.3*idense*asum01              ; Feedforward
adel01  delay   asum01+.3*idense*asub01,.008*idecay  ; Feedback

; All-Pass 2
asub11  =       adel11-.3*idense*asub01              ; Feedforward
adel11  delay   asub01+.3*idense*asub11,.012*idecay  ; Feedback

adel21  delay   asub11, .004*idecay                   ; Delay 1
adel41  delay   adel21, .017*idecay                   ; Delay 2

; Single Nested All-Pass
asum51  =       adel52-.25*adel51*idense              ; Inner Feedforward
aout51  =       asum51-.50*adel41*idense              ; Outer Feedforward
adel51  delay   adel41+.50*aout51*idense, .025*idecay ; Outer Feedback
adel52  delay   adel51+.25*asum51*idense, .062*idecay ; Inner Feedback

adel61  delay   aout51, .031*idecay                   ; Delay 3
adel81  delay   adel61, .003*idecay                   ; Delay 4

; Double Nested All-Pass
asum91  =       adel92-.25*adel91*idense              ; First  Inner Feedforward
asum92  =       adel93-.25*asum91*idense              ; Second Inner Feedforward
aout91  =       asum92-.50*adel81*idense              ; Outer Feedforward
adel91  delay   adel81+.50*aout91*idense, .120*idecay ; Outer Feedback
adel92  delay   adel91+.25*asum91*idense, .076*idecay ; First  Inner Feedback
adel93  delay   asum91+.25*asum92*idense, .030*idecay ; Second Inner Feedback

aout    =       .8*aout91+.8*adel61+1.5*adel21 ; Combine outputs

        zaw      aout*kdclick, ioutch

        endin

;----------------------------------------------------------------------------------
; Output For reverb
       instr  90

idur   =      p3
igain  =      p4
iinch  =      p5

kdclik linseg 0, .002, igain, idur-.004, igain, .002, 0

ain    zar    iinch
       outs   ain*kdclik, -ain*kdclik  ; Inverting one side makes the sound
       endin                           ; seem to come from all around you.
                                       ; This may cause some problems with certain
                                       ; surround sound systems

;----------------------------------------------------------------------------------
; Output For reverb
       instr  91

idur   =      p3
igain  =      p4
iinch1 =      p5
iinch2 =      p6

kdclik linseg 0, .002, igain, idur-.004, igain, .002, 0

ain1   zar    iinch1
ain2   zar    iinch2
       outs   ain1*kdclik, ain2*kdclik

       endin

       instr   99
       zacl    0,30
       endin

</CsInstruments>
<CsScore>
;-------------------------------------------------------------------------------
; Reverbs derived from those presented by Bill Gardner
; using nested all-pass filters.
; Coded by Hans Mikelson 1998
;-------------------------------------------------------------------------------

; 1. Noise Click
; 2. Disk Input Mono
; 3. Disk Input Stereo
; 8. Simple Sum
; 9. Feedback Filter
;10. Delay
;11. Simple All-Pass Filter
;12. Nested All-Pass Filter
;13. Double Nested All-Pass Filter
;15. Output
;17. 2D First Echo
;21. Small Room Reverb
;22. Medium Room Reverb
;23. Large Room Reverb
;25. Small Room Reverb with controls
;26. Medium Room Reverb with controls
;27. Large Room Reverb with controls

f1  0 65536  10 1
f2  0 65536  10 1 .1 .2  .4  .1  .5

;-------------------------------------------------------------------------------
;   Sta  Dur  Amp  Pitch  SoundIn  OutCh
;i2  0.0  2.7  0.0  .5     5      1

; Small Room Reverb
;    Sta  Dur  Amp  InCh
;i21  0.0  2.2  1.0  1

; Medium Room Reverb
;    Sta  Dur  Amp  InCh
;i22  0.0  2.5  1.0  1

; Large Room Reverb
;    Sta  Dur  Amp  InCh
;i23  0.0  2.8  1.0  1

; Sound In Stereo
;   Sta  Dur  Amp  Pitch  SoundIn  OutCh1  OutCh2
;i3  0.0  8.69 0.8  1      11       1       2

; Sound In Mono
;   Sta  Dur  Amp  Pitch  SoundIn  OutCh
;i2  0.0  1.0  0.0  1      11       1

; Simple Sine
;   Sta  Dur  Amp    Pitch  Table  OutCh
;i6  0.0  1.7  10000  9.00   2      1

; Simple Sum
;  Sta  Dur  InCh1  InCh2  OutCh
;i8 0    4.0  1      2      3

; Sound In Stereo
;   Sta  Dur   Amp  Pitch  SoundIn  OutCh
i2  0.0  20.43 0.8  .3628  "vocestereo.aiff"      1

; 2D Echos X, Y in meters
;    Sta  Dur  Amp  EarX  EarY  SourceX  SourceY  WallX  WallY  InCh  OutCh1  OutCh2
i17  0.0  22   4    10    12    34       17       46     60     1     2       3

;Large Room Reverb
;    Sta  Dur  Amp  InCh  Decay  Densty1  Densty2  PreFilt  HiPass  LoPass  OutCh
i27  0.0  22   .5   2     1.50   .80      1.4      10000    5100    200     6
i27  0.0  22   .5   3     1.52   .82      1.5      10100    5000    210     7
;i27  0.0  5.0  .2   4     1.50   .80      1.4      10000    5100    200     8
;i27  0.0  5.0  .2   5     1.52   .82      1.5      10100    5000    210     9

; Reverb Mixer
;    Sta  Dur  Amp  InCh1  InCh2
i91  0    22   1    6      7
;i91  0    5.0  1    8      9

; Clear Zak
;    Sta  Dur
i99  0.0  22


</CsScore>
</CsoundSynthesizer>
