<CsoundSynthesizer>
<CsOptions>
-odac -d
</CsOptions>
<CsInstruments>
; Converted from PNF.ORC and PNF.SCO
; Original files preserved in same directory

sr        =         44100
kr        =         44100
ksmps     =         1


          zakinit   2,2

          instr 1
idur      =         1/150
          timout    0,idur,noise
          turnoff
noise:
kamp      linen     1,idur/5,idur,idur/5
anoise    rand      kamp

; ANOISE OSCILI KAMP,150,1; SEE HOW A PNF CORRUPTS
; AN INNOCENT SINE WAVE ;-)

          zaw       anoise,0
          endin

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
instr 2; PASSIVE NONLINEAR FILTER
;AFTER J.PIERCE & SCOTT V. DUYNE / US PATENT 5,703,313
;CODED BY JOSEP M COMAJUNCOSAS /NOVÂ´98
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
iatt1     =         .998                                    ; attenuation
ifco1     =         -.058                                   ; freq dependent decay

aback     init      0
afdbk     init      0

anoise    zar       0
ainput1   =         anoise + aback
aout1     delay     ainput1,1/150
alpf      filter2   aout1*iatt1, 1, 1, 1+ifco1, ifco1

; PASSIVE NONLINEAR FILTER : A VARIABLE ALLPASS FILTER
; REQUIRES SR = KR TO RUN THE CONDITIONAL

; COMPUTE SOME PARAMETERS
au        =         alpf - afdbk
;au       =         adcrem - afdbk                          ; ??????sona millor!!!
ku        downsamp  au
if ku < 0 goto or
kstiff    =         p4
goto next
or:
kstiff    =         p5
next:
afdbk     delay1    kstiff*au
; THE FILTER ITSELF
alpf2     biquad    alpf,kstiff,1,0,1,kstiff,0

ainput2   =         alpf2
aback     delay     ainput2,1/150

          out       aout1*10000
          zacl      0,2
          endin

</CsInstruments>
<CsScore>
f1 0 8193 10 1

i1 0 1
i2 0 5.5 -.3 -.2
s
i1 0 1
i2 0 5.5 .35 .32
s
i1 0 1
i2 0 5.5 -.85 -.55
s
i1 0 1
i2 0 5.5 -.35 -.32
s
i1 0 1
i2 0 5.5 -.15 .15
s
i1 0 1
i2 0 5.5 .05 .65
s
e

</CsScore>
</CsoundSynthesizer>
