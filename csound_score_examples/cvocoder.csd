<CsoundSynthesizer>
<CsOptions>
-odac -d
</CsOptions>
<CsInstruments>
; Converted from cvocoder.orc and cvocoder.sco
; Original files preserved in same directory

sr             =         44100
kr             =         4410
ksmps          =         10


               zakinit   10,10

instr          1                             ; "SOLINA" STRING ENSEMBLE AS THE CARRIER
kamp           linen     1, 1,p3,1
kamp2          linen     1, 2,p3,1
kpitch         init      cpspch(p4)

alfo1          lfo       .001,.8
alfo2          lfo       .001,.56

abuzz1         buzz      kamp, kpitch*(1+alfo1), sr/(2*kpitch), 1 ,0
asaw1          filter2   abuzz1, 1, 1, 1, -.95                        ; WEAK BASS RESPONSE
abuzz2         buzz      kamp, kpitch*(1-alfo2), sr/(2*kpitch), 1 ,0
asaw2          filter2   abuzz2, 1, 1, 1, -.95                        ; WEAK BASS RESPONSE
abuzz3         buzz      kamp2, 2*kpitch, sr/(4*kpitch), 1 ,0         ; 8TH HIGHER
asaw3          filter2   abuzz3, 1, 1, 1, -.999

amix           =         .25*(asaw1+asaw2+2*asaw3)

;ADD SOME CHORUS
adel1          lfo       .01, .8
adel1          =         .04*(1+adel1)
adel2          lfo       .03, .7
adel2          =         .04*(1+adel2)
adel3          lfo       .02, .9
adel3          =         .04*(1+adel3)
aflanger1      flanger   amix, adel1, 0, .1
aflanger2      flanger   amix, adel2, 0, .1
aflanger3      flanger   amix, adel3, 0, .1

amix2          =         .5*amix + .2*(aflanger1+aflanger2+aflanger3)
;amix2         gauss     1                                            ;TEST WITH NOISE CARRIER
zawm           amix      2,0
;              out       amix2*4000
endin

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
instr          2         ; ANOTHER ANALOG VOCODER
;CODED BY JOSEP M COMAJUNCOSAS / NOVÂ´98
; FEATURES : 8 BANDS WITH ASSIGNABLE CARRIER & MODULATOR INPUTS
; FLEXIBLE ROUTING WITH THE ZAK SYSTEM AND SELECTABLE FREQ. WITH TABLE L.U.
; IMPROVED FREQ. RESPONSE WITH THE ADDITION OF AN UNVOCODED HI.FREQ. SIGNAL
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;TO GET THE MOST OF THIS INSTRUMENT
;RECORD YOUR VOICE SLOWLY AND CLEARLY
;REDUCT NOISE, COMPRESS A LOT AND NORMALISE
;ADDING REVERB TO YOUR VOICE
;BEFORE VOCODING CAN BE REALLY COOL. TRY IT!

;USER SETTINGS HERE ********
;ROUTING TABLE
irft           =         2
;FREQ          TABLE
ichfft         =         6
;FILTER POLES
ipoles         =         6
;RMS RACKING
iperiod        =         60;123
;HP DRY MIX
ihpdry         =         .4

;MODULATOR INPUT
amod           soundin   "hellorcb2.aif"
amod           butterhp  amod,30
;carrier       input
acarr          zar       0
;***************************

;CHANNELS FREQ. SETUP
if0            table     0,ichfft
if1            table     1,ichfft
if2            table     2,ichfft
if3            table     3,ichfft
if4            table     4,ichfft
if5            table     5,ichfft
if6            table     6,ichfft
if7            table     7,ichfft

;if0           =         cpspch(if0)
;if1           =         cpspch(if1)
;if2           =         cpspch(if2)
;if3           =         cpspch(if3)
;if4           =         cpspch(if4)
;if5           =         cpspch(if5)
;if6           =         cpspch(if6)
;if7           =         cpspch(if7)

;COMPUTE BANDWIDTHS
ibw1           =         if2-if0
ibw2           =         if3-if1
ibw3           =         if4-if2
ibw4           =         if5-if3
ibw5           =         if6-if4
ibw6           =         if7-if3

;ANALYSE MODULATOR
am0            tonex     amod,if0,ipoles
am1            resonx    amod,if1,ibw1,ipoles,1
am2            resonx    amod,if2,ibw2,ipoles,1
am3            resonx    amod,if3,ibw3,ipoles,1
am4            resonx    amod,if4,ibw4,ipoles,1
am5            resonx    amod,if5,ibw5,ipoles,1
am6            resonx    amod,if6,ibw6,ipoles,1
am7            atonex    amod,if7,ipoles

;GET RMS FROM EACH MOD. BAND
krms0          rms       am0, iperiod
krms1          rms       am1, iperiod
krms2          rms       am2, iperiod
krms3          rms       am3, iperiod
krms4          rms       am4, iperiod
krms5          rms       am5, iperiod
krms6          rms       am6, iperiod
krms7          rms       am7, iperiod

;WRITE RMS TO ZAK SPACE
zkw            krms      0,0
zkw            krms      1,1
zkw            krms      2,2
zkw            krms      3,3
zkw            krms      4,4
zkw            krms      5,5
zkw            krms      6,6
zkw            krms      7,7

;ANALYSE CARRIER
ac0            tonex     acarr,if0,ipoles
ac1            resonx    acarr,if1,ibw1,ipoles,1
ac2            resonx    acarr,if2,ibw2,ipoles,1
ac3            resonx    acarr,if3,ibw3,ipoles,1
ac4            resonx    acarr,if4,ibw4,ipoles,1
ac5            resonx    acarr,if5,ibw5,ipoles,1
ac6            resonx    acarr,if6,ibw6,ipoles,1
ac7            atonex    acarr,if7,ipoles

;ROUTING SETUP
ir0            table     0,irft
ir1            table     1,irft
ir2            table     2,irft
ir3            table     3,irft
ir4            table     4,irft
ir5            table     5,irft
ir6            table     6,irft
ir7            table     7,irft

;BAND ROUTING
krmsr0         zkr       ir0
krmsr1         zkr       ir1
krmsr2         zkr       ir2
krmsr3         zkr       ir3
krmsr4         zkr       ir4
krmsr5         zkr       ir5
krmsr6         zkr       ir6
krmsr7         zkr       ir7

;BALANCE CARRIER W. MOD. SIGNAl
ab0            gain      ac0, krmsr0,iperiod
ab1            gain      ac1, krmsr1,iperiod
ab2            gain      ac2, krmsr2,iperiod
ab3            gain      ac3, krmsr3,iperiod
ab4            gain      ac4, krmsr4,iperiod
ab5            gain      ac5, krmsr5,iperiod
ab6            gain      ac6, krmsr6,iperiod
ab7            gain      ac7, krmsr7,iperiod

;MIX ALL BALANCED BANDS
; + SOME OF THE ORIGINAL 7TH CHANNEL
;FOR BETTER INTELIGIBILITY
amix           =         .125*(ab0+ab1+ab2+ab3+ab4+ab5+ab6+ab7+ihpdry*am7)

               out       amix*10

               zacl      0,0                      ;CLEAR BEFORE NEXT PASS
endin

</CsInstruments>
<CsScore>
f1 0 32768 10 1

;BAND ROUTING : NORMAL SETTINGS
f2 0 8 -2 0 1 2 3 4 5 6 7
;INVERTED SETTINGS
f3 0 8 -2 7 6 5 4 3 2 1 0
;RANDOM SETTINGS (YOU CAN EVEN REPEAT A CHANNEL)
f4 0 8 -2 1 6 7 4 3 5 2 0
f5 0 8 -2 0 0 1 1 5 7 6 4

;VOCODER BAND FREQUENCIES
;f6 0 8 -2 7.09 8.05 9.01 9.09 10.05 11.01 11.09 12.05 13.01
;f6 0 8 -2 6.09 7.05 8.01 8.09 9.05 10.01 10.09 11.05 12.01
f6 0 8 -2 200  265 390 550 800 1200 1770 2650 3900 4600

i1 0 4 8.00 .24
i1 0 . 8.07 .42
i1 0 . 9.02 .23
i1 0 . 9.05 .41
i1 0 . 9.10 .14

i1 3.9 4 7.07 .24
i1 . . 8.05 .42
i1 . . 8.10 .23
i1 . . 9.05 .41
i1 . . 9.09 .14

i1 7.8 4 7.10 .24
i1 . . 8.09 .42
i1 . . 9.02 .23
i1 . . 9.05 .41
i1 . . 9.07 .14

i1 11.7 4 7.05 .24
i1 . . 8.09 .42
i1 . . 8.10 .23
i1 . . 9.02 .41

i2 0 17
e
; A PENTATONIC MODULATION
;NICE AURAL EFFECT
;RESSEMBLES A VOCODER
i1 4 8 7.00 .4
i1 4 8 9.02 .2
i1 0 8 8.00 .24
i1 0 8 8.07 .42
i1 0 6 9.02 .23
i1 0 5 9.05 .41
i1 0 5 9.10 .14
i1 3 5 9.04 .46
i1 3 5 9.09 .29
i1 6 5 9.04 .86
i1 6 5 10.07 .67
i1 6 5 6.00 .36
i1 6 5 7.07 .82
i2 0 13
e

</CsScore>
</CsoundSynthesizer>
